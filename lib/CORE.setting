# vim: ft=perl6

my module CORE;

sub slurp($path) { Q:CgOp { (box Str (slurp (unbox str (@ {$path})))) } }

sub spew($path,$text) { Q:CgOp { (rnull (spew (unbox str (@ {$path.Str})) (unbox str (@ {$text.Str})))) } }

my class IO is Cool {
}

my class TextReader is IO {
    method get() {
        Q:CgOp {
            (letn line (treader_getline (unbox treader (@ {self})))
              (ternary (== (l line) (null str)) {Str} (box Str (l line))))
        };
    }

    method slurp() {
        Q:CgOp { (box Str (treader_slurp (unbox treader (@ {self})))) }
    }

    method getc() {
        Q:CgOp {
            (letn chi (treader_getc (unbox treader (@ {self})))
              (ternary (>= (l chi) (int 0))
                (box Str (str_chr (l chi)))
                {Str}))
        };
    }

    method lines() {
        gather take my $l while ($l = self.get).defined;
    }
}

sub open($filename) {
    Q:CgOp { (box TextReader (treader_open (obj_getstr {$filename}))) }
}

$PROCESS::IN ::= Q:CgOp { (box TextReader (treader_stdin)) };
@PROCESS::ARGS = unitem(Q:CgOp { (box Parcel (getargv)) });

{YOU_ARE_HERE}
